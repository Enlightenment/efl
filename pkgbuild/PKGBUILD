# Maintainer: Enlightenment Developers <enlightenment-devel@enlightenment.org>
pkgname=('efl-git' 'efl_doc-git')
_pkgname=${pkgname%-*}
pkgver=1.8.99.24004.0b86e51
pkgrel=1
pkgdesc="Enlightenment Foundation Libraries - GIT development snapshot (Ecore, Eldbus, Edje, Eet, Eeze, Efreet, Eina, Eio, Embryo, Emotion, Eo, Ephysics, Ethumb, & Evas)"
arch=('i686' 'x86_64' 'arm')
url="http://www.enlightenment.org"
license=('BSD' 'LGPL2.1' 'GPL2' 'custom')
makedepends=('doxygen' 'check')
depends=('bullet' 'libpng' 'libjpeg-turbo'
         'gstreamer' 'gst-plugins-base-libs' 'gst-plugins-base'
         'gst-plugins-good' 'gst-plugins-bad'
         'zlib' 'lua' 'libtiff' 'openssl' 'util-linux' 'curl' 'dbus'
         'glibc' 'fontconfig' 'freetype2' 'fribidi' 'libpulse'
         'libsndfile' 'systemd' 'libx11' 'libxau' 'libxcomposite'
         'libxdamage' 'libxdmcp' 'libxext' 'libxfixes' 'libxinerama'
         'libxi' 'libxrandr' 'libxrender' 'libxss' 'libxtst'
         'libxcursor' 'libxp' 'libgl' 'giflib' 'libwebp' 'harfbuzz'
	 'wayland' 'sdl' 'avahi')
[[ ! $(pacman -T "openjpeg") ]] && depends+=('openjpeg') #jpeg2k loader is autodetected at build time
makedepends=('git')
provides=("ecore=$pkgver" "eldbus=$pkgver" "edje=$pkgver"
          "eet=$pkgver" "eeze=$pkgver" "efreet=$pkgver"
          "eina=$pkgver" "eio=$pkgver" "embryo=$pkgver" "emotion=$pkgver"
          "ephysics=$pkgver" "ethumb=$pkgver" "evas=$pkgver" "$_pkgname=$pkgver")
conflicts=('ecore' 'edje' 'eet' 'eeze' 'efreet' 'eina' 'eio' 'embryo' 'emotion'
           'ethumb' 'evas' "$_pkgname")
options=('!libtool' 'debug')
install=efl.install

pkgver() {
  cd "../.."

  local efl_version=$(grep -m 1 EFL_VERSION configure.ac | grep -o "[[:digit:]]*" | tr '\n' '.')
  efl_version=$(awk -F , -v efl_version=${efl_version%.} '/^AC_INIT/ {gsub(/efl_version/, efl_version); gsub(/[\[\] -]/, ""); print $2}' configure.ac)

  printf "$efl_version.$(git rev-list --count HEAD).$(git rev-parse --short HEAD)"
}

prepare() {
  sed -i 's/env python$/&2/' "../../src/scripts/eina/eina-bench-cmp"
}

build() {
  cd "../.."
  export CFLAGS="$CFLAGS -fvisibility=hidden -fomit-frame-pointer"
  export CXXFLAGS="$CXXFLAGS -fvisibility=hidden -fomit-frame-pointer"
  [[ -e Makefile ]] && make clean distclean
  ./configure --prefix=/usr \
  --disable-static --disable-tslib --enable-fb \
  --enable-xinput22 --enable-multisense --enable-systemd \
  --enable-image-loader-webp --enable-harfbuzz \
  --enable-wayland --enable-drm --enable-sdl
  make
  make -j1 doc
}

#check() {
#  cd "../.."
#  make -k check
#}

package_efl-git() {
  cd "../.."
  make -j1 DESTDIR="$pkgdir/" install
  install -Dm644 README "$pkgdir/usr/share/$pkgname/README"
  install -Dm644 NEWS "$pkgdir/usr/share/$pkgname/NEWS"
  install -Dm644 ChangeLog "$pkgdir/usr/share/$pkgname/ChangeLog"
  install -Dm644 AUTHORS "$pkgdir/usr/share/$pkgname/AUTHORS"
  install -Dm644 COMPLIANCE "$pkgdir/usr/share/$pkgname/COMPLIANCE"
  install -Dm644 licenses/COPYING.BSD "$pkgdir/usr/share/licenses/$pkgname/COPYING.BSD"
  install -Dm644 licenses/COPYING.GPL "$pkgdir/usr/share/licenses/$pkgname/COPYING.GPL"
  install -Dm644 licenses/COPYING.LGPL "$pkgdir/usr/share/licenses/$pkgname/COPYING.LGPL"
  install -Dm644 licenses/COPYING.SMALL "$pkgdir/usr/share/licenses/$pkgname/COPYING.SMALL"
  ln -s "../licenses/$pkgname/COPYING.BSD" "$pkgdir/usr/share/$pkgname/COPYING.BSD"
  ln -s "../licenses/$pkgname/COPYING.GPL" "$pkgdir/usr/share/$pkgname/COPYING.GPL"
  ln -s "../licenses/$pkgname/COPYING.LGPL" "$pkgdir/usr/share/$pkgname/COPYING.LGPL"
  ln -s "../licenses/$pkgname/COPYING.SMALL" "$pkgdir/usr/share/$pkgname/COPYING.SMALL"
}

package_efl_doc-git() {
  pkgdesc="Documentation for the Enlightenment Foundation Libraries"
  arch=('any')
  unset license depends provides conflicts install
  cd "../.."
  mkdir -p "$pkgdir/usr/share/doc/$pkgname"
  cp -a doc/html "$pkgdir/usr/share/doc/$pkgname/html"
  cp -a doc/latex "$pkgdir/usr/share/doc/$pkgname/latex"
}

